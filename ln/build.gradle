apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        minSdkVersion 4
        targetSdkVersion 22
        versionCode 3
        versionName "1.1.2"
    }
    buildTypes {
    }
}

def siteUrl = 'https://github.com/niranjan94/ln'
def gitUrl = 'https://github.com/niranjan94/ln.git'
version = "1.1.2"
group = "com.niranjan.ln"

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'jar'

                name 'Ln - Android Logging Simplified'
                description 'Ln is a simple wrapper for Android\'s default Log implementation (android.util.Log). It is derived from RoboGuice\'s internal logging utility.'
                url siteUrl

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'niranjan94'
                        name 'Niranjan Rajendran'
                        email 'niranjan94@yahoo.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl

                }
            }
        }
    }
}

dependencies {

}


android.libraryVariants.all { variant ->
    def name = variant.buildType.name
    if (!name.equals("debug")) {
        def task = project.tasks.create "${name}-${version}", Jar
        task.dependsOn variant.javaCompile
        task.from variant.javaCompile.destinationDir
        artifacts.add('archives', task);
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}
javadoc.mustRunAfter sourcesJar

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
javadocJar.mustRunAfter javadoc

artifacts {
    archives javadocJar
    archives sourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    def ossUser = user
    def ossPassword = properties.getProperty("oss.password")

    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "Ln-AndroidLoggingSimplified"
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        publish = true
        licenses = ['Apache-2.0']

        version {
            gpg {
                sign = true //Determines whether to GPG sign the files. The default is false
            }
            //Optional configuration for Maven Central sync of the version
            mavenCentralSync {
                sync = false //Optional (true by default). Determines whether to sync the version to Maven Central.
                user = ossUser //OSS user token
                password = ossPassword //OSS user password
            }
        }

    }
}
bintrayUpload.mustRunAfter javadoc
